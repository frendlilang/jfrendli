
----------
START RULE
----------

file:
	statement* END_OF_FILE ;


---------------
STATEMENT RULES
---------------

statement:
	classDecl
	| classFieldDecl
	| functionDecl
	| variableDecl
	| changeStmt
	| expressionStmt
	| ifStmt
	| repeatTimesStmt
	| repeatWhileStmt
	| returnStmt
	| returnWithStmt ;

classDecl:
	"describe" IDENTIFIER ( "inherit" IDENTIFIER )? classBlock ;

classFieldDecl:
	"has" IDENTIFIER ( "=" expression )? NEWLINE ;

functionDecl:
	"define" IDENTIFIER "(" parameters? ")" block ;

variableDecl:
	"create" IDENTIFIER "=" expression NEWLINE ;

changeStmt:
	"change" ( call "." )? IDENTIFIER "=" expression NEWLINE ;

expressionStmt:
	expression NEWLINE ;

ifStmt:
	"if" expression block ( "otherwise" block )? ;

repeatTimesStmt:
	"repeat" expression "times" block ;

repeatWhileStmt:
	"repeat" "while" expression block ;

returnStmt:
	"return" NEWLINE ;

returnWithStmt:
	"return" "with" expression NEWLINE ;

block:
	NEWLINE INDENT statement+ DEDENT ;

classBlock:
	NEWLINE INDENT classFieldDecl* functionDecl+ DEDENT ;


------------
HELPER RULES
------------

parameters:
	"accept" IDENTIFIER ( "," IDENTIFIER )* ;

arguments:
	"send" expression ( "," expression )* ;


----------------
EXPRESSION RULES
----------------

expression:
	logicOr ;

logicOr:
	logicAnd ( "or" logicAnd )* ;

logicAnd:
	comparison ( "and" comparison )* ;

comparison:
	term ( ( "<" | "<=" | ">" | ">=" | "equals" | "unequals" ) term )* ;

term:
	factor ( ( "+" | "-" ) factor )* ;

factor:
	unary ( ( "*" | "/" ) unary )* ;

unary:
	( "not" | "-" ) unary | call ;

call:
	primary ( "(" arguments? ")" | "." IDENTIFIER )* ;

primary:
	IDENTIFIER
	| NUMBER
	| TEXT
	| "true"
	| "false"
	| "empty"
	| "me"
	| "parent" "." IDENTIFIER
	| "(" expression ")" ;


-------------
LEXICAL RULES
-------------

IDENTIFIER:
	ALPHA ( ALPHA | DIGIT )* ;

NUMBER:
	DIGIT+ ( "." DIGIT+ )? ;

TEXT:
	"\"" <any ASCII character except "\"" or NEWLINE>* "\"" ;

ALPHA:
	"a" ... "z"
	| "A" ... "Z"
	| "_" ;

DIGIT:
	"0" ... "9" ;

NEWLINE:
    "\\n"
    | "\\r\\n"
    | "\\r" ;

INDENT:
    ( " " | "\\t" )+ ;

DEDENT:
    <a decrease in INDENT> ;
